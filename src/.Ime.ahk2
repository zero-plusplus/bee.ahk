class Ime {
  /**
   * Get a boolean value indicating whether the IME is enabled or disabled.
   * @type {boolean}
   */
  static status {
    get {
      return this.get()
    }
  }
  /**
   * Get a boolean value indicating whether the IME is enabled or disabled.
   * @return {boolean}
   */
  static get() {
    static IMC_GETOPENSTATUS := 0x0005

    return bee.toBoolean(this._sendMessage(IMC_GETOPENSTATUS))
  }
  /**
   * Set the IME status.
   * @param {boolean} status - Enable if `true`, disable if `false`.
   * @throws Error - Throw an exception if the state setting fails.
   */
  static set(status) {
    static IMC_SETOPENSTATUS := 0x006

    errorCode := this._sendMessage(IMC_SETOPENSTATUS, status)
    if (0 < errorCode) {
      throw Error("Failed to set status. code: " errorCode)
    }
  }
  static _sendMessage(wParam, lParam := 0) {
    static WM_IME_CONTROL := 0x0283

    return DllCall(
      "SendMessage",
      "UInt", DllCall("imm32\ImmGetDefaultIMEWnd", "Uint", WinActive("A")),
      "UInt", msg := WM_IME_CONTROL,
      "Int", wParam,
      "Int", lParam
    )
  }
}